WHITESPACE = _{ " " | NEWLINE }
Separator  = _{ WHITESPACE | NEWLINE }

Document    = { SOI ~ Declaration* ~ EOI }
Declaration = { Definition | Seq | NamedSeq }

Definition = { Identifier ~ "=" ~ Expression }
Identifier = { (LETTER | NUMBER | "_")+ }
Number     = { NUMBER+ }
Expression = ${ "(" ~ Token* ~ (Separator+ ~ Token)* ~ ")" }
Path       = { "/" ~ Identifier ~ ("/" ~ Identifier)* ~ "/"? }
Token      = { Identifier | Path }

NamedSeq   = { Identifier ~ ":" ~ Seq }
Seq        = ${ SeqHead ~ SeqBody? ~ SeqTail }

SeqHead    = { "[" ~ Separator* ~ Duration? ~ Separator* }
Duration   = ${ Number ~ ";" ~ Separator? }

SeqBody    = ${ Step ~ (Separator+ ~ Step)* }
Step       = { Rest | Hit }
Rest       = { "." }
Hit        = { Identifier ~ (And ~ Identifier)* }
And        = _{ Separator* ~ "&" ~ Separator* }

SeqTail    = ${ Separator * ~ "]" ~ SeqEnder* }
SeqEnder   = { Repeats | Divide }
Repeats    = ${ "*" ~ Number }
Divide     = ${ "/" ~ Number }

