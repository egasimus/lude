WHITESPACE = _{ " " | NEWLINE }
Separator  = _{ WHITESPACE | NEWLINE }

Document    = { SOI ~ Declaration* ~ EOI }
Declaration = { Definition | Seq | NamedSeq }

Definition = { Term ~ "=" ~ (Expression | Resource) }
Expression = { "(" ~ ExprToken* ~ (Separator+ ~ ExprToken)* ~ ")" }
ExprToken  = { Term | Resource }
Term       = ${ (LETTER | NUMBER | "_" | ".")+ }
Resource   = { Protocol ~ ":" ~ Path }
Protocol   = { "sound" }
Path       = { ("/"+ | "./") ~ Term ~ ("/" ~ Term)* ~ "/"? }

NamedSeq   = { Term ~ ":" ~ Seq }
Seq        = ${ SeqHead ~ SeqBody? ~ SeqTail }

SeqHead    = { "[" ~ Separator* ~ Duration? ~ Separator* }
Duration   = ${ NUMBER+ ~ ";" ~ Separator? }

SeqBody    = ${ Step ~ (Separator+ ~ Step)* }
Step       = { Rest | Hit }
Rest       = { "." }
Hit        = { Term ~ (And ~ Term)* }
And        = _{ Separator* ~ "&" ~ Separator* }

SeqTail    = ${ Separator * ~ "]" ~ SeqEnder* }
SeqEnder   = { Repeats | Divide }
Repeats    = ${ "*" ~ NUMBER+ }
Divide     = ${ "/" ~ NUMBER+ }
