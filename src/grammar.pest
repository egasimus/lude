WHITESPACE = _{ " " | NEWLINE }
Separator  = _{ WHITESPACE | NEWLINE }

Document    = { SOI ~ Declaration* ~ EOI }
Declaration = { Definition | Seq | NamedSeq }

Definition = { Word ~ "=" ~ Expression }
Expression = { "(" ~ ExprToken* ~ (Separator+ ~ ExprToken)* ~ ")" }
Word       = ${ (LETTER | NUMBER | "_" | ".")+ }
ExprToken  = { Word | Path }
Path       = { "/" ~ Word ~ ("/" ~ Word)* ~ "/"? }

NamedSeq   = { Word ~ ":" ~ Seq }
Seq        = ${ SeqHead ~ SeqBody? ~ SeqTail }

SeqHead    = { "[" ~ Separator* ~ Duration? ~ Separator* }
Duration   = ${ NUMBER+ ~ ";" ~ Separator? }

SeqBody    = ${ Step ~ (Separator+ ~ Step)* }
Step       = { Rest | Hit }
Rest       = { "." }
Hit        = { Word ~ (And ~ Word)* }
And        = _{ Separator* ~ "&" ~ Separator* }

SeqTail    = ${ Separator * ~ "]" ~ SeqEnder* }
SeqEnder   = { Repeats | Divide }
Repeats    = ${ "*" ~ NUMBER+ }
Divide     = ${ "/" ~ NUMBER+ }
